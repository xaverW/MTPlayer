plugins {
    id 'application'
    id "org.openjfx.javafxplugin" version "0.1.0"
    id 'com.gradleup.shadow' version '9.3.0'
}
javafx {
    version = "21.0.5"
    modules = ['javafx.controls']
}
repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

//<><><><><><><><><><><><><><><><><><><><>
def programName = 'MTPlayer'
def jarName = 'MTPlayer.jar'
def theMainClass = 'de/p2tools/mtplayer/Main'
// def theMainModul = 'mtplayer'
//<><><><><><><><><><><><><><><><><><><><>

def jre_linux = '../../ADMIN/JDK/jre_linux/'
def jre_windows = '../../ADMIN/JDK/jre_windows/'

def res_all = './res/res_all'
def res_linux = './res/res_linux'
def res_windows = './res/res_windows'
def res_mac = './res/res_mac'

def fatJarDir = layout.buildDirectory.get().toString() + "/fatJar" + "/"
def releaseDir = './release/'
def distDir = "./dist/"

def propertyFile = "./src/main/resources/version.properties"
def versionNo = loadProgProperties().getProperty('VERSION').toInteger()
def buildNo = loadProgProperties().getProperty('BUILD').toInteger()
def newDate = new Date().format('yyy.MM.dd')

def buildDir_All = layout.buildDirectory.get().toString() + "/" + programName + "/"
def buildDir_Linux = layout.buildDirectory.get().toString() + "/" + programName + "_mit_Java__Linux/"
def buildDir_Win = layout.buildDirectory.get().toString() + "/" + programName + "_mit_Java__Win/"

// ===========================================
// build project
// ===========================================
group = 'de.p2tools'
application {
    mainClass = theMainClass
}

dependencies {
    //<><><><><><><><><><><><><><><><><><><><>
    implementation ':commons-cli-1.9.0'
    implementation ':commons-io-2.18.0'
    implementation ':commons-lang3-3.17.0'

    implementation ':jackson-core-2.18.2'
    implementation ':jackson-databind-2.18.2'
    implementation ':jackson-annotations-2.18.2'

    implementation ':xz-1.10'
    implementation 'org.jsoup:jsoup:1.18.3'

    implementation("com.squareup.okhttp3:okhttp:4.12.0")

    //<><><><><><><><><><><><><><><><><><><><>
    implementation ':ikonli-core-12.4.0'
    implementation ':ikonli-javafx-12.4.0'
    implementation ':ikonli-fluentui-pack-12.4.0'

    implementation 'org.kordamp.ikonli:ikonli-material-pack:12.4.0'
    implementation 'org.kordamp.ikonli:ikonli-material2-pack:12.4.0'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign-pack:12.4.0'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign2-pack:12.4.0'
    //<><><><><><><><><><><><><><><><><><><><>


    implementation ':p2lib'
    implementation ':controlsfx-11.2.2'

    implementation "org.openjfx:javafx-fxml:21:linux"
    implementation "org.openjfx:javafx-controls:21:linux"
    implementation "org.openjfx:javafx-graphics:21:linux"
    implementation "org.openjfx:javafx-base:21:linux"

    implementation "org.openjfx:javafx-fxml:21:win"
    implementation "org.openjfx:javafx-controls:21:win"
    implementation "org.openjfx:javafx-graphics:21:win"
    implementation "org.openjfx:javafx-base:21:win"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    manifest {
        attributes("Manifest-Version": "1.0",
                "Main-Class": theMainClass);
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
tasks.register('fatJar', Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest.from jar.manifest
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    destinationDirectory.set(file(fatJarDir))
    archiveFileName = jarName
    with jar
}
tasks.shadowJar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE // Or WARN.
    mergeServiceFiles()

    destinationDirectory.set(file(fatJarDir))
    archiveFileName = jarName
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Zip).configureEach { task ->
    task.doLast {
        //ant.checksum file: archiveFile.get().asFile
        ant.checksum(algorithm: 'SHA-512', file: archiveFile.get().asFile)
    }
}

tasks.register('updateDate') {
    // und nur dann speichern
    ant.propertyfile(file: propertyFile) {
        entry(key: "VERSION", value: versionNo)
        entry(key: "BUILD", value: buildNo)
        entry(key: "DATE", value: newDate)
    }
    println("")
    println("= updateDate =============================")
    println("VERSION: $versionNo")
    println("Build: $buildNo")
    println("DATE: $newDate")
    println("========================================")
    println("")
}
tasks.register('updateProjectInfos') {
    doLast {
        // und nur dann speichern
        ant.propertyfile(file: propertyFile) {
            entry(key: "VERSION", value: versionNo)
            entry(key: "BUILD", value: buildNo + 1)
            entry(key: "DATE", value: newDate)
        }
        println("")
        println("= updateProjectInfos =============================")
        println("VERSION: $versionNo")
        println("Build: $buildNo")
        println("DATE: $newDate")
        println("========================================")
        println("")
    }
}

def loadProgProperties() {
    Properties props = new Properties()
    file('src/main/resources/version.properties').withInputStream {
        props.load(it)
    }
    return props
}

tasks.register('dist') {
    dependsOn clean, jlinkZip
}
defaultTasks 'dist'

tasks.register('cleanDist', Delete) {
    delete distDir
    delete releaseDir
}
clean.dependsOn cleanDist, updateDate

// ============================================
// build dist dir and zipFile
// ============================================
tasks.register("copyAll") {
    dependsOn shadowJar
    doFirst {
        copy {
            from(res_all)
            from(res_linux)
            from(res_windows)

            from file(fatJarDir + jarName)
            into file(buildDir_All)
        }
        copy {
            from file(propertyFile)
            into file(buildDir_All + "Info")
            rename("version.properties", "version.txt")
        }
        copy {
            from file(buildDir_All)
            into file(distDir)
        }
    }
}
tasks.register('buildZip_all', Zip) {
    dependsOn clean, copyAll
    destinationDirectory = layout.projectDirectory.dir(releaseDir)
    from layout.projectDirectory.dir(buildDir_All)
    into programName
    archiveFileName = programName + '-' + versionNo + '-' + buildNo + '__' + new Date().format('yyyy.MM.dd') + '.zip'
}

// ============================================
// build jre linux and zipFile
// ============================================
tasks.register("copyLinux") {
    dependsOn buildZip_all
    doFirst {
        copy {
            from(jre_linux) {}
            into file(buildDir_Linux + "Java")
        }
        copy {
            from(res_all)
            from(res_linux)

            from file(fatJarDir + jarName)
            into file(buildDir_Linux)
        }
        copy {
            from file(propertyFile)
            into file(buildDir_Linux + "Info")
            rename("version.properties", "version.txt")
        }
    }
}
tasks.register('buildZip_linux', Zip) {
    dependsOn copyLinux
    archiveFileName = programName + '-' + versionNo + '-' + buildNo + '__Linux=mit=Java__' + new Date().format('yyyy.MM.dd') + '.zip'
    destinationDirectory = file(releaseDir)
    from fileTree(buildDir_Linux) {
        into programName + "__Linux+Java"
    }
}

// ============================================
// build jre windows and zipFile
// ============================================
tasks.register("copyWindows") {
    dependsOn buildZip_all
    doFirst {
        copy {
            from(jre_windows)
            into file(buildDir_Win + "Java")
        }
        copy {
            from(res_all)
            from(res_windows)

            from file(fatJarDir + jarName)
            into file(buildDir_Win)
        }
        copy {
            from file(propertyFile)
            into file(buildDir_Win + "Info")
            rename("version.properties", "version.txt")
        }
    }
}
tasks.register('buildZip_windows', Zip) {
    dependsOn copyWindows
    archiveFileName = programName + '-' + versionNo + '-' + buildNo + '__Windows=mit=Java__' + new Date().format('yyyy.MM.dd') + '.zip'
    destinationDirectory = file(releaseDir)
    from fileTree(buildDir_Win) {
        into programName + "__Windows+Java"
    }
}


// ============================================
// build SRC zipFile
// ============================================
tasks.register('buildZip_src', Zip) {
    from projectDir
    include 'src/**/*'
    include 'res/**/*'
    include 'libs/**/*'
    archiveFileName = programName + '-' + versionNo + '-' + buildNo + '__SRC__' + new Date().format('yyyy.MM.dd') + '.zip'
    destinationDirectory = file(releaseDir)
}


// ============================================
// build
// ============================================
tasks.register('onlyDist') {
    //only the distDir
    dependsOn buildZip_all, buildZip_src
}

tasks.register('jreDist') {
    //only the distDir
    dependsOn buildZip_all, buildZip_linux, buildZip_windows, buildZip_src
}
